#compdef git-delete-submodule
#description delete submodule from config and working directory

function _git-delete-submodule() {
  local expl
  local -a submodules
  local gitdir=$(git rev-parse --git-dir)
  local update_policy

  zstyle -s ":completion:$curcontext:" cache-policy update_policy
  [[ -z "$update_policy" ]] && zstyle ":completion:$curcontext:" cache-policy _git_delete_submodule_caching_policy

  if _cache_invalid  "git-delete-submodules-path-$gitdir" || ! _retrieve_cache "git-delete-submodules-path-$gitdir"; then
    submodules=${(z)$(git submodule | awk '{ print $2 }')}
    _store_cache "git-delete-submodules-path-$gitdir" submodules
  fi

  _wanted submodules expl submodule compadd - $submodules
}

function _git_delete_submodule_caching_policy() {
  local gitmodules="$(git rev-parse --show-toplevel)/.gitmodules"
  [[ -f "$gitmodules" && "$gitmodules" -nt "$1" ]] && return 0

  # Rebuild if cache is older than one week.
  local -a oldp
  oldp=( "$1"(Nmw+1) )
  (( $#oldp )) && return 0
  
  return 1
}

_git-delete-submodule $*
