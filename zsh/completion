autoload -U compinit
compinit -i

zmodload -i zsh/complist

# matches case insensitive for lowercase
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

# provide .. as a completion
zstyle ':completion:*' special-dirs ..

# pasting with tabs doesn't perform completion
zstyle ':completion:*' insert-tab pending

# use caching
zstyle ':completion::complete:*' use-cache
zstyle ':completion::complete:*' cachepath ./cache/

# Use a menu for completion
zstyle ':completion:*:*:*:*:*' menu select

# Describe options in full
zstyle ':completion:*:options' auto-description '%d'
zstyle ':completion:*:options' description 'yes'

# Make the process selection a little nicer
zstyle ':completion:*:*:kill:*:processes' list-colors  '=(#b) #([0-9]#) ([0-9a-z-]#)*=01;34=0=01'
zstyle ':completion:*:*:*:*:processes' command "ps -au $USER -o pid,user,comm -w -w"

# Load known hosts file for auto-completion with ssh and scp commands
function {
  local -a _ssh_hosts
  local -a _etc_hosts
  local -a hosts
  [ -r ~/.ssh/known_hosts ] && _ssh_hosts=(${${${${(f)"$(<$HOME/.ssh/known_hosts)"}:#[\|]*}%%\ *}%%,*}) || _ssh_hosts=()
  [ -r /etc/hosts ] && : ${(A)_etc_hosts:=${(s: :)${(ps:\t:)${${(f)~~"$(</etc/hosts)"}%%\#*}##[:blank:]#[^[:blank:]]#}}} || _etc_hosts=()
  hosts=("$_ssh_hosts[@]" "$_etc_hosts[@]" $(hostname) localhost)
  zstyle ':completion:*:hosts' hosts $hosts
  zstyle ':completion:*:*:(ssh|scp):*:*' hosts $hosts
}

# Don't ever select the parent directory, e.g. cd ../<TAB> won't select your current dir
zstyle ':completion:*:cd:*' ignore-parents parent pwd

# With commands like rm, it's annoying if you keep getting offered the same
# file multiple times. This fixes it. Also good for cp, et cetera..
zstyle ':completion:*:rm:*' ignore-line yes
zstyle ':completion:*:cp:*' ignore-line yes

function _force_rehash () {
  (( CURRENT == 1 )) && rehash
  return 1 # Because we didn't really complete anything
}

zstyle -e ':completion:*' completer '
if [[ $_last_try != "$HISTNO$BUFFER$CURSOR" ]] ; then
  _last_try="$HISTNO$BUFFER$CURSOR"
  reply=(_complete _match _ignored _prefix _files)
else
  if [[ $words[1] == (rm|mv) ]] ; then
    reply=(_complete _files)
  else
    reply=(_oldlist _expand _force_rehash _complete _ignored _correct _approximate _files)
  fi
fi'

# Some completion settings
# zstyle ':completion:*' completer _complete _match _approximate
zstyle ':completion:*:match:*' original only

# Expand partial paths
# zstyle ':completion:*' expand 'prefix'
zstyle ':completion:*' squeeze-slashes 'yes'

# verbose completion
zstyle ':completion:*' verbose true
zstyle ':completion:*:descriptions' format '%F{green}Completions for %B%d%b%f'
zstyle ':completion:*:messages' format '%d'
zstyle ':completion:*:warnings' format 'No matches for: %d'
zstyle ':completion:*:corrections' format '%B%d (errors: %e)%b'
zstyle ':completion:*:correct:*' original true
zstyle ':completion:*:correct:*' insert-unambiguous true
zstyle ':completion:*:correct:' prompt 'Correct to: %e'

# Separate matches into groups
zstyle ':completion:*' group-name ''

# Ignore completions functions for commands you don't have
zstyle ':completion::(^approximate*):*:functions' ignored-patterns '_*'

# Make approximate allow more errors as the length increases
zstyle -e ':completion:*:approximate:*' max-errors 'reply=($((($#PREFIX+$#SUFFIX)/3))numeric)'

# Suggested completion style from _git completion
zstyle ':completion::*:git-{name-rev,add,rm}:*' ignore-line true

# Add a recent director listing completion using the cdr command
autoload -Uz chpwd_recent_dirs cdr add-zsh-hook
add-zsh-hook chpwd chpwd_recent_dirs
zstyle ':completion:*:*:cdr:*:*' menu selection
zstyle ':chpwd:*' recent-dirs-default true
zstyle ':completion:*' recent-dirs-insert both
zstyle ':chpwd:*' recent-dirs-prune parent

function {
  emulate -L zsh
  local hash
  for hash in md5 sha1 sha256 sha512; compdef _files $hash
}

[[ $IS_MAC = 1 ]] && compdef _man pman

# vim: filetype=zsh
